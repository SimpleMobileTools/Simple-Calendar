apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.timfreiheit.resourceplaceholders'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.simplemobiletools.calendar.pro"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 221
        versionName "6.19.0"
        multiDexEnabled true
        setProperty("archivesBaseName", "calendar")
        vectorDrawables.useSupportLibrary = true
        resConfigs "en", "ar", "az", "be", "bg", "bn", "br", "ca", "cs", "da", "de", "el", "eo", "es", "et", "eu", "fi", "fr", "gl", "hi-rIN", "hr", "hu", "in", "it", "iw", "ja", "ko", "lt", "lv", "ml", "nb-rNO", "nl", "pl", "pt", "pt-rBR", "ro", "ru", "sk", "sv", "th", "tr", "uk", "zh-rCN", "zh-rHK", "zh-rTW"
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "variants"
    productFlavors {
        core {}
        fdroid {}
        prepaid {}
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    resourcePlaceholders {
        files = ['xml/shortcuts.xml']
    }
}

dependencies {
    implementation 'com.github.SimpleMobileTools:Simple-Commons:b2a2c40a27'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.print:print:1.0.0"

    kapt 'androidx.room:room-compiler:2.4.3'
    implementation 'androidx.room:room-runtime:2.4.3'
    annotationProcessor 'androidx.room:room-compiler:2.4.3'
}
